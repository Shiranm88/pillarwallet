// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "33.0.0"
        minSdkVersion = 28
        compileSdkVersion = 33
        targetSdkVersion = 33
        supportLibVersion = "28.0.0"
        multidex_version = "2.0.1" // custom multidex
        firebaseIidVersion = "19.0.0" // required by react-native-device-info for getting device ID
        kotlinVersion = "1.6.0" // react-native-webview
    }
    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' } // @react-native-firebase/crashlytics
    }
    dependencies {
        classpath("com.android.tools.build:gradle:4.1.3")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files

        classpath 'com.google.gms:google-services:4.3.10' // @react-native-firebase/app ini, react-native-push-notification
        classpath 'io.fabric.tools:gradle:1.28.1' // @react-native-firebase/crashlytics
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion" // react-native-webview
        classpath 'com.google.firebase:perf-plugin:1.3.1' // // @react-native-firebase/perf
    }
}

def REACT_NATIVE_VERSION = new File(['node', '--print',"JSON.parse(require('fs').readFileSync(require.resolve('react-native/package.json'), 'utf-8')).version"].execute(null, rootDir).text.trim())

allprojects {
    configurations.all {
        resolutionStrategy {
            // Remove this override in 0.65+, as a proper fix is included in react-native itself.
            force "com.facebook.react:react-native:" + REACT_NATIVE_VERSION
        }
    }
    repositories {
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        google()
        jcenter()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }

        // other custom repos:
        maven { url 'https://maven.google.com' }
        // TODO: check if needed on 0.59.x, but can be removed after RN >=0.6x:
//        configurations.all {
//            resolutionStrategy {
//                force "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
//            }
//        }
    }
}

buildscript {
    configurations.all {
        resolutionStrategy {
            eachDependency {
                // https://issuetracker.google.com/issues/109894262#comment9
                if (requested.group == "org.jetbrains.trove4j" && requested.name == "trove4j" && requested.version == "20160824") {
                    useTarget("org.jetbrains.intellij.deps:trove4j:1.0.20181211")
                }
            }
        }
    }
}
